plugins {
    id 'dev.gradleplugins.java-gradle-plugin' version '1.1.35'
    id 'dev.nokee.experimental.bintray-publish' version '0.5.0-430640a7'
    id 'dev.gradleplugins.gradle-plugin-unit-test' version '1.1.35'
    id 'dev.gradleplugins.gradle-plugin-functional-test' version '1.1.35'
}

group = 'dev.nokee.init'
version = '0.8.0' // Change version in NokeeInitPlugin as well

tasks.named('processResources', ProcessResources) { task ->
    task.from(file('nokee.init.gradle')) {
        into('dev/nokee/init/internal/wrapper')
    }
}

repositories {
    jcenter()
    gradlePluginDevelopment()
}

gradlePlugin {
    compatibility {
        minimumGradleVersion = '6.2.1'
    }
    java {
        withJavadocJar()
        withSourcesJar()
    }
}

def lombokVersion = '1.18.12'
def mockitoVersion = '3.6.0'
def junitVersion = '5.7.0'
dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

test {
    dependencies {
        implementation spockFramework()
        implementation groovy()
        implementation 'net.bytebuddy:byte-buddy:1.10.14'
        implementation 'cglib:cglib-nodep:3.3.0'
        implementation 'org.objenesis:objenesis:3.1'

        // Lombok
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        // JUnit 5
        implementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
        runtimeOnly "org.junit.vintage:junit-vintage-engine:${junitVersion}"
        implementation "org.junit-pioneer:junit-pioneer:1.3.0"

        // Mockito
        implementation "org.mockito:mockito-core:${mockitoVersion}"

		implementation 'org.hamcrest:hamcrest-core:2.2'
		implementation "com.spotify:hamcrest-optional:1.2.0"

		implementation 'com.google.guava:guava-testlib:30.1-jre'
    }
}

functionalTest {
    testingStrategies = [strategies.coverageForMinimumVersion, strategies.coverageForLatestGlobalAvailableVersion, strategies.coverageForLatestNightlyVersion]
    dependencies {
        implementation spockFramework()
        implementation groovy()
        implementation gradleFixtures()

        implementation "commons-io:commons-io:2.8.0"

        // Lombok
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        // JUnit 5
        implementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
        runtimeOnly "org.junit.vintage:junit-vintage-engine:${junitVersion}"

        // Mockito
        implementation "org.mockito:mockito-core:${mockitoVersion}"
    }
    testTasks.configureEach { Test task ->
        task.systemProperty('dev.nokee.init.gold-nokee-init-script', file('nokee.init.gradle').absolutePath)

        if (task.name == 'functionalTestMinimumGradle') {
            task.enabled = (JavaVersion.current().majorVersion != '14')
        }
    }
}

tasks.withType(Test).configureEach { it.useJUnitPlatform() }

publishing {
    repositories {
        maven {
            name = 'bintray'
            url = 'https://dl.bintray.com/nokeedev/distributions-initialization'
            ext.packageName = 'dev.nokee.init'
            credentials(PasswordCredentials)
        }
		maven {
			name = 'nokeeRelease'
			url = 's3://repo-nokee-dev-release'
			credentials(AwsCredentials)
		}
    }
}

tasks.register('release') {
    dependsOn('publishAllPublicationsToBintrayRepository')
}
